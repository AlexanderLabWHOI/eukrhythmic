configfile: "config.yaml"

import io
import os
import sys
from snakemake.exceptions import print_exception, WorkflowError
import sys
sys.path.insert(1, '../scripts')
from importworkspace import *

def get_samples(assemblygroup):
    samplelist = list(SAMPLEINFO.loc[SAMPLEINFO['AssemblyGroup'] == assemblygroup]['SampleID']) 
    if assemblygroup == "merged":
        samplelist = list(SAMPLEINFO['SampleID'])
    return samplelist
    
    
if DROPSPIKE == 0:
    LEFTFILE = lambda filename: expand(os.path.join(OUTPUTDIR, "firsttrim", "{samples}_1.trimmed.fastq.gz"), samples = get_samples(filename.assembly))
    RIGHTFILE = lambda filename: expand(os.path.join(OUTPUTDIR, "firsttrim", "{samples}_2.trimmed.fastq.gz"), samples = get_samples(filename.assembly))
else:
    LEFTFILE = lambda filename: expand(os.path.join(OUTPUTDIR, "bbmap", "{samples}_1.clean.fastq.gz"), samples = get_samples(filename.assembly))
    RIGHTFILE = lambda filename: expand(os.path.join(OUTPUTDIR, "bbmap", "{samples}_2.clean.fastq.gz"), samples = get_samples(filename.assembly))
    
rule salmon:
    input: 
        trinityfile = os.path.join(OUTPUTDIR, "trinity_results_assembly_{assembly}", "Trinity.fasta"),
        left = lambda filename: expand(os.path.join(OUTPUTDIR, "firsttrim", "{samples}_1.trimmed.fastq.gz"), samples = get_samples(filename.assembly)),
        right = lambda filename: expand(os.path.join(OUTPUTDIR, "firsttrim", "{samples}_2.trimmed.fastq.gz"), samples = get_samples(filename.assembly))
    output:
        directory(os.path.join(OUTPUTDIR, "salmon_quant_assembly_{assembly}"))
    params:
        libtype = "A",
        indexname = os.path.join(OUTPUTDIR, "salmon_index_assembly_{assembly}"),
        decoysfile = "decoys_{assembly}.txt",
        kval = 31
    log:
        err = os.path.join("logs","salmon","orig_{assembly}_err.log"),
        out = os.path.join("logs","salmon","orig_{assembly}_out.log")
    conda: "../envs/salmon-env.yaml"
    shell:
        """
        salmon index -t {input.trinityfile} -i {params.indexname} -k {params.kval} 2> {log.err} 1> {log.out}
        salmon quant -i {params.indexname} -l {params.libtype} -1 {input.left} -2 {input.right} --validateMappings -o {output} 2>> {log.err} 1>> {log.out}
        """
        
rule salmon_indiv:
    input: 
        fastafile = os.path.join(OUTPUTDIR, "assembled", "{assembly}_{assembler}.fasta"),
        left = LEFTFILE,
        right = RIGHTFILE
    output:
        directory(os.path.join(OUTPUTDIR, "salmon_indiv", "salmon_quant_assembly_{assembly}_{assembler}"))
    params:
        libtype = "A",
        indexname = os.path.join(OUTPUTDIR, "salmon_indiv", "salmon_index_assembly_{assembly}_{assembler}"),
        kval = 31
    log:
        err = os.path.join("logs","salmon","indiv_{assembly}_{assembler}_err.log"),
        out = os.path.join("logs","salmon","indiv_{assembly}_{assembler}_out.log")
    conda: "../envs/salmon-env.yaml"
    shell:
        """
        salmon index -t {input.fastafile} -i {params.indexname} -k {params.kval} 2> {log.err} 1> {log.out}
        salmon quant -i {params.indexname} -l {params.libtype} -1 {input.left} -2 {input.right} --validateMappings -o {output} 2>> {log.err} 1>> {log.out}
        """
    
rule salmon_clust3:
    input: 
        assemblyfile = os.path.join(OUTPUTDIR, "cluster3", "{assembly}_transdecoded.fasta"),
        left = LEFTFILE,
        right = RIGHTFILE
    output:
        directory(os.path.join(OUTPUTDIR, "salmon", "salmon_quant_assembly_{assembly}"))
    params:
        libtype = "A",
        indexname = os.path.join(OUTPUTDIR, "salmon_index_assembly_{assembly}"),
        decoysfile = "decoys_{assembly}.txt",
        kval = 31
    log:
        err = os.path.join("logs","salmon","merged_{assembly}_err.log"),
        out = os.path.join("logs","salmon","merged_{assembly}_out.log")
    conda:
        "../envs/salmon-env.yaml"
    shell:
        """
        salmon index -t {input.assemblyfile} -i {params.indexname} -k {params.kval} 2> {log.err} 1> {log.out}
        salmon quant -i {params.indexname} -l {params.libtype} -1 {input.left} -2 {input.right} --validateMappings -o {output} 2>> {log.err} 1>> {log.out}
        """
